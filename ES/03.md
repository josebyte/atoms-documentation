#Atoms
## 3 - Molecule: Agrupando átomos
Ya conoces lo que es el elemento *Atom* pero como sabes el desarrollo de aplicaciones se basa en la composicion de elementos complejos para dar una mayor funcionalidad. Por ejemplo un `Atom.Button` por si solo no tiene mucha funcionalidad pero si lo unimos, por ejemplo, con un `Atom.Input` podemos crear una funcionalidad como poder insertar el valor del input, o buscar un termino... En esencia un elemento `Molecule` es un agrupador de elementos `Atom` y controla todo los comportamientos entre ellos para a su vez el mismo lanzar eventos para que otros sistemas mayores (Organism) o hermanos (Molecule, Atom) reaccionen. Vamos a crear nuestra primera Molécula:

```
class Form extends Atoms.Class.Molecule
  @template : """<form {{#if.style}}class="{{style}}"{{/if.style}}></form>"""

  @available: ["Atom.Label", "Atom.Input", "Atom.Textarea", "Atom.Select", "Atom.Switch", "Atom.Button"]

  @events   : ["change", "submit", "error"]

  @base     : "Form"

  @defaults :
    events: ["submit"]
```

Como puedes comprobar su definición es muy parecida al elemento Atom aunque en este caso dispone del atributo  @available, el cual establece los tipo de elementos que puede contener blindando asi su funcionalidad (nunca un desarrollador podrá introducir algo que no esperamos en nuestra molecula). Ahora creemos nuestra primera instancia `Form`:

```
form = new Form events: ["change", "error"]
```

Ya conoces, desde el capítulo anterior, como establecer atributos a una instancia y tambien sobreescribir los valores por defectos; pero lo que no sabes es como establecer instancias `Atom` dentro de la molecula. Para ello tendrémos que utilizar el atributo `AppendChild`. Este recibe dos parametros el primero es el nombre de la clase a instanciar y el segundo los atributos con los que queremos instanciarla: 

```
form.appendChild "Atom.Input", {type: "search", placeholder: "type your search..."}
form.appendChild "Atom.Button", {text: "Search near me...", events: ["touch"]}
```

El pseudo-dom se encargará de representar una estructura HTML:

```
<form>
  <input type="search" placeholder: "Type your search..." />
  <button>
    <span class="icon search"></span>
    <abbr>Search near me...</abbr>
  </button>
</form>
```

Como puedes ver según se va creando una estructura el HTML comienza a complicarse y por lo tanto comienza a ser más dificilmente mantenible. De todas formas volvamos al método AppendChild, como puedes ver sirve para crear dinámicamente elementos `Atom` los cuales podemos destruir con el método `destroyChildren`:

```
form.destroyChildren()
```

Tal vez te interese crear moleculas con elementos Atom ya establecidos no utilizando el metodo `AppendChild`, para ello vamos a aprenderlo por medio de un doble ejercicio. Vas a aprender como crear tu primera molecula (Search) extendida de otra Molecula (Form) la cual va a tener elementos por defecto y como puedes hacerte una idea utilizarémos para ello el atributo `@children` del atributo especial `@default`: 

```
class Search extends Form

  @available: ["Atom.Input", "Atom.Button"]

  @events   : ["change", "submit"]

  @extends  : true

  @default  :
    events  : ["submit"]
    children: [
      "Atom.Input": id: "input", type: "search", placeholder: "Type your search...", events: ["keypress", "keyup"], required: true
    ,
      "Atom.Button": icon: "search"
    ]
```

Si estudiamos un poco el código vemos que `Search` extiende de `Form` y que sobreescribe varios atributos:

  - @available: Ahora solo permite dos tipos de Atom: `Input` y `Button`
  - @events: Ahora solo tiene 2 eventos *"change"* y *"Submit"*
  - @default: Aquí podemos ver como establecer los *hijos* de una Molécula

A su vez es necesario establecer el atributo `@extends` a `true` para que Atoms pueda establecer correctamente la herencia al pseudo-DOM (en posteriores capítulos será de vital importancia para el sistema de events). Tras esta definición podríamos crear una instancia de Search:

```
search = new Search()
```

Representandola:

```
<form>
  <input type="search" placeholder: "Type your search..." />
  <button class="icon">
    <span class="icon search"></span>
  </button>
</form>
```

Como puedes comprender esto hace que la reutilización de la Molécula Search sea mucho más sencilla puesto que no tenemos que definir, una y otra vez, sus átomos. Ahora veamos como recoger los eventos que lanzan los atomos Input y Button, para ello Atoms nos ofrece un sistema automático de publicación de eventos basados en el paradigma *Bubbling/Tuneling* que veremos más adelante. Como puedes ver el `Atom.Input` tiene establecido los eventos *"keypress"* y *"keyup"* y nuestra molecula se enterará cuando nos suscribamos a un evento que lanza Atoms automáticamente en este caso `onInputKeypress` y `onInputKeyup` que sigue la convención establecida: on + [Elemento] + [Evento]. Ahora veamos como y donde alojar este código en nuestra molécula:

```
class Search extends Form

  onInputKeyup: (event, atom) =>
    @bubble "change", event unless event.keyCode is 13
    false

  onInputKeypress: (event, atom) =>
    @bubble "submit" event, atom if event.keyCode is 13
    false
 
```

En este caso son eventos *Bubble* ya que se disparan desde el elemento más básico (atom) hasta el más complejo (molecule) recibiendo varios parametros, en este caso `event` y `atom` que hacen referencia a la información del evento y al elemento que ha disparado el evento. En el caso de los eventos de teclado recogidos en `Atom.Input` en event nos viene un atributo `keyCode` el cual define el valor númerico de la tecla pulsada. Por lo tanto la molecula `Search` cada vez que se dispare el Keyup del input lanzará un evento bubble *"change"* que se convertirá en el pseudo-DOM como `onSearchChange` listo para ser tratado por los elementos *padre* de la molecula. En el caso de que se dispare el evento Keypress y si el KeyCode es 13 dispararémos el evento bubble `onSearchSubmit`.

Aunque nunca hayas trabajado con este sistema de eventos puedes comprobar que es realmente sencillo, ya que el pseudoDOM se encarga del *naming* de los eventos (más adelante verémos como sobreescribirlos). Únicamente tenemos que utilizar el método @bubble para lanzar eventos burbuja acompañado del nombre del evento y de la información que queramos emitir. En posteriores capítulos verémos como customizar los eventos Bubble a su vez manejaremos los eventos Tunel.

Otra parte interesante es poder acceder a una instancia de Atom desde una instancia Molecule, para ello nos fijarémos en el primer objeto del attributo especial `children`:

```
"Atom.Input": 
	id 				: "input", 
	type 			: "search", 
	placeholder 	: "Type your search...", 
	events 		: ["keypress", "keyup"], 
	required 		: true
```

Si nos fijamos el atributo `id` puedes hacer una idea de lo que puede hacer, no es otra cosa que crear un simbolo con el nombre "input" dentro de la instancia de la molecula, de esta manera podríamos recogerla en su ambito por ejemplo en el evento onInputKeyup:

```
  onInputKeyup: (event, atom) =>
    @input.el.attr "disabled", true
    ...
```

Con el simbolo `@input` tenemos acceso a la instancia y por lo tanto podemos acceder a todos sus atributos y funcionalidades, de todas formas justo en el evento onInputKeyup al ser un evento de tipo `Bubble` nos llega el Atom que ha disparado el mismo; por lo que el código podría quedar de la siguiente manera:

```
  onInputKeyup: (event, atom) =>
    atom.el.attr "disabled", true
	...
```

Con esto hemos terminado la introducción a los elementos *Molecule*, como puedes ver genera un nivel más de control 