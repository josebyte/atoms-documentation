#Atoms
## 5 - Entity: Dando valor a los átomos

El elemento *Entity* debes tomárlo como la M del patrón MVC, es el encargado de valor y contenido al resto de tus elementos. En el caso de Atoms se ha intentado simplificar expresandolo como "Entity -> Atom", o lo que es lo mismo se intenta llevar el lenguaje natural al mundo de la programación por lo que solo tienes que pensar que una `Entity` solo pueden establecerse en los elementos del tipo *Atom*, ya que realmente es la únidad mínima. Por ejemplo; una `Molecule.List` contiene una serie de instancias `Atom.Li` pero en si no tendría valor si cada una de estas no tuviese una *Entity* asignada (una lista de contactos por ejemplo).

En este capítulo aprenderemos como crear y utilizar el elemento *Entity* sin tener en cuenta en que *Atom* esté asignado, eso lo dejarémos para el capítulo destinado al control de Eventos.


### 5.1 Definición de una entidad
El elemento Entity no tiene nada que ver con lo visto hasta ahora ya que en esta ocasión cada nueva instancia no existe vinculo álguno con el DOM, y por lo tanto el Pseudo-DOM no hace acto de presencia. Piensa en Entity como en el  en el valor mínimo de información de tu Aplicación, piensa en el como en el *dato*, ahora veamos como crear nuestra primera definición:

```
class Task extends Atoms.Class.Entity
  @fields "name", "description", "type", "done"
```

Como vemos nuestra nueva clase `Task` extiende de `Atoms.Class.Entity`, la cual trae consigo todas las funcionalidades necesaria para el control de entidades. En nuestra clase contenemos 4 campos: `"name"`, `"description"`, `"type"` y `"done"` que son los valores que podemos almacenar en cada instancia de `Task`. Ahora creemos nuestra primera instancia:

```
task = new Task()
task.name = "Clean my teeth"
task.type = "Home"
task.save()
```

En el momento de ejecutar el método `save`, Atoms guardará en memoria esa instancia para poder tratarla posteriormente. De todas formas existen otras 2 formas de instanciar nuestra clase `Task`, asignando los atributos en su propio constructor:

```
task = new Task name: "Go to the meeting", type: "Work"
task.save()
```

O utilizando el método interno `create`:

```
Task.create name: "Learn CoffeeScript", type: "Personal"
```

Ahora vamos a aprender como crear atributos especiales dentro de una determinada Entity, estos son muy utiles cuando queremos combinar atributos base (`"name"`, `"description"`, `"type"` y `"done"`):

```
class Task extends Atoms.Class.Entity
  @fields "name", "description", "type", "done"
  
  @summary: ->
    "#{@name} - #{@description}"
```

En este caso hemos creado el atributo especial `@summary`, el cual no deja de ser una función (`->`) la cual concatena los valores internos de `name` y `description`. Ahora veamos otro caso algo más dinámico:

```
class Task extends Atoms.Class.Entity
  ...
  
  @style: -> if @done then "success" else "pending"
```

Como ves hemos creado un nuevo atributo especial llamando `@style` el cual devuelve un estilo en base al valor que tenga el atributo `@done`, `"success"` si es *true* `"pending"` si es *false*. Una manera de asegurar la integridad de los datos de una `Entity` es mediante el método interno `validate`, validaciones, pudiendo blindar la creación de instancias si no cumple la regla contenida. Por ejemplo vamos a limitar la creación de instancias Task sino ha rellenado el atributo "name":

```
class Task extends Atoms.Class.Entity
  ...
  
  validate: ->
    unless @name
      "name is required"      
```

De esta manera si ahora creamos instancias:

```
task = Task.create name: "Go to the meeting", done: true
> created

task = Task.create type: "work"
> "name is required"
```

Si queremos saber si la función `validate` ha funcionado correctamente vamos a comenzar a ver como Atoms gestiona las instancias en memoria para ello vamos a utilizar el método interno `all`:

```
for task in Task.all()
  console.log task.name
> "Go to the meeting"

Task.create name: "Learn CoffeeScript", type: "Personal"

for task in Task.all()
  console.log task.name
> "Go to the meeting"
> "Learn CoffeeScript"
```

Como podemos ver el método interno `all` nos devuelve una lista de instancias `task` la cual podemos recorrer, y en el caso del ejemplo imprimimos por consola el nombre de cada una de ellas. En el caso de que quisiesemos obtener una determinada instancia, o un grupo de ellas, Atoms ofrece una serie de métodos que te facilitarán esa tarea. Por ejemplo vamos a seleccionar todas las tareas que esten terminadas:

```
Task.select (task) -> task if done is true
```

Como vemos el método `select` recorre toda las tareas y por cada una de ellas ejecuta la expresión `done is true` en el caso de que sea cierta este método lo alojará en un array para su posterior lectura. Ahora vamos a recordar los atributos especiales y vamos a crear uno nuevo utilizando esta función:

```
class Task extends Atoms.Class.Entity
  ...
  
  @allDone: ->
    @select (task) -> !!task.done
```

El nuevo atributo especial `allDone` realiza internamente una selección de instancias de la clase `Task` al cual podemos llamar de la siguiente manera:

```
for task in Task.allDone()
  console.log task.name
> "Go to the meeting"
```

En el caso de que no busquemos obtener una lista y queramos acceder a una determinada instancia podemos utilizar los métodos `find` o `findBy`, mientras el primero se basa en un codigo único e interno que asigna Atoms a cada instancia llamado UID, el segundo sirve para buscar por un determinado atributo interno. Veamos ambos:

```
task = Task.findBy "name", "Learn CoffeeScript"
console.log task.uid
> "935E8B41-3C1C-4BE8-815C-5FCD42DF05BC"

task = Task.find "935E8B41-3C1C-4BE8-815C-5FCD42DF05BC"
console.log task.name
> "Learn CoffeeScript"
```

Como vemos el método `findBy` recibe 2 parametros, el primero contiene el nombre del atributo a utilizar para la búsqueda y el segundo el valor de la misma. Para el método find es algo más sencillo ya que unicamente necesitamos el valor UID de la instancia.

Bien ya conoces como crear instancias, asignar atributos especiales, filtrar las instancias asignadas en memoria e incluso varios mecanismos para encontrar una deteminada instancia. Atoms te da tambien la posibilidad tanto de actualizar como de eliminar determinadas instancias asignadas en memoria. Vamos a comenzar a modificar una instancia Task:

```
task = Task.create name: "Learn CoffeeScript", type: "Personal"
console.log task.type
> "Personal"

task.type = "Work"
task.done = true
task.save()
console.log task.type
> "Work"
```

Realmente ya conocías el método `save`, unicamente no sabías que lo podías utilizar siempre que lo necesites para actualizar una determinada instancia. En el caso de que quisieses realizar la tarea de actualización en una sola linea más expresiva puedes utilizar el método `updateAttributes`:
	
```
task.updateAttributes type: "Work", done: true
```

El método `updateAttributes` recibe un objeto con todos los nuevos valores que queremos asignar a determinados atributos, no necesariamente todos. Ahora veamos como eliminar instancias de *Entity*:

```
task.destroy()
```	

Realmente únicamente tenemos que llamar al método `destroy` el cual eliminará de memoria la instancia en concreto. En el caso de que quisieramos eliminar todas las instancias contenidas de la clase Task podemos utilizar el método interno `destroyAll`:

```
console.log Task.count()
> 3

Task.destroyAll()
console.log Task.count()
> 0
```

Antes de llamar al método `destroyAll` te enseñamos otro método interno `count`, que nos devuelve el numero de instancias almacenadas en memoria (algo muy util para crear contadores). Tras la ejecución de `destroyAll` vemos como el método `count` nos devuelve `0`, demostrando que la memoria ha conseguido ser liberada correctamente.

Con esto acabamos este capítulo, en próximos capítulos veremos como utilizar los elementos *Entity* combinados con *Atom* y *Molecule* utilizando el sistema de eventos que ofrece Atoms.