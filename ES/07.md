#Atoms
## 7 - Extensiones

Hasta ahora hemos aprendido el *core* de Atoms, hemos descubierto que existe un pseudo-DOM encargado de la gestión de elementos y de su representación final en el DOM. Hemos aprendido que existen elementos de representación como son: *Atom*, *Molecule* y *Organism* y que existe un elemento especial, *Entity*, que es quien almacena la unidad informativa de tu aplicación. Todos ellos se comunican por un sistema propio de eventos que puede tener diferentes tipologías dependiendo de las necesidades de tu aplicación: *Bubbling*/*Tuneling*, *Bind*/*Trigger*, *Entity Binding*... 

Ahora con todo lo aprendido podemos comenzar con la verdadera funcionalidad de Atoms, la creación atómica por medio de extensiones. La creación atómica se basa en la reutilización de los elementos básicos (*Atom*, *Molecule* y *Organism*) y su combinación para la creación de nuevos elementos extendidos que a su vez pueden volver a reutilizarse. De esta manera cualquier extensión que crees podrás reutilizarla en todo momento independientemente de las futuras versiónes de Atoms que utilices, ya que al basarse en extensiones de elementos básicos seguirá cumpliendo las interfaces implicitas. Con esto, en un futuro próximo la programación Web cambiará con la llegada de los llamados [Web Components](http://webcomponents.org/), cuando estos sean una realizada existirá una nueva versión del Core de Atoms la cual será totalmente compatible con todas las extensiones creadas hasta el momento.


### 7.1 - Nuestra primera extensión
Ahora vamos a crear nuestra primera extensión gracias a todo lo aprendido en todos los capítulos anteriores. En este caso vamos a crear una extensión que gestione la conocida etiqueta `<form>`, como sabemos debe ser un elemento del tipo Molecule, puesto que es un contenedor de elementos más básicos y para esta primera versión necesitarémos crear:

   - *Atom*: `<input/>` y `<button/>`
   - *Molecule*: `<form/>`

Sabemos que un formulario puede contener más elementos como `<label>`, `<fieldset>`,... pero queremos que profundices en la creación extendida de elementos con lo mínimo posible, el resto de extensiones de Atom te la dejamos para tus ratos libres :)

### 7.2 - Atom.Input
Vamos a comenzar con la creación de la etiqueta `<input/>` la cual como sabemos puede tener diferentes tipos: `"text"`, `"password"`, `"range"` y como queremos que sea lo más dinámica posible crearemos su definición con un template lo suficientemente extensible:

```
class Atoms.Atom.Input extends Atoms.Class.Atom

  @template : """
    <input type="{{type}}" name="{{name}}" {{#if.placeholder}}placeholder="{{placeholder}}"{{/if.placeholder}} {{#if.style}}class="{{style}}"{{/if.style}} {{#if.value}}value="{{value}}"{{/if.value}} {{#required}}required{{/required}} {{#disabled}}disabled{{/disabled}} />"""

  @base     : "Input"
```

Como sabemos y gracias al motor de renderización [Mustache](http://mustache.github.io/) podemos crear todos los *Binding Properties*, `{{}}`,  que necesitemos y en nuestro caso para la representación en el DOM podrémos establecer los atributos: `type`, `placeholder`, `style`, `value`, `required` y `disabled`. Recuerda que tenemos el pseudo-DOM por lo que podremos establecer otros atributos para su utilización en cada instancia, los cuales nunca serán representados en el DOM. Comencemos a crear unas instancias de prueba para ver su representación:

```
text = new Atoms.Atom.Input type: "text", placeholder: "Your name..."
text.el.html()
> <input type="text" placeholder="Your name..." />

password = new Atoms.Atom.Input type: "password", required: true
password.el.html()
> <input type="password" required />

number = new Atoms.Atom.Input type: "number", value: 1980
number.el.html()
> <input type="number" value="1980" />
```

Como podemos comprobar el resultado final en el DOM es el que buscamos, ahora vamos a asignar los `@events` permitidos para nuestro `Atom.Input` asi como algun valor por defecto estableciendo `@default`:

```
class Atoms.Atom.Input extends Atoms.Class.Atom
  ... 
  
  @events   : ["keypress", "keyup"]
    
  @default  :
    type: "text"
```

Ahora nuestro `Atom.Input` es capaz de desplegar los eventos *Bubble*: `"keypress"` y `"keyup"`, recordando que realmente Atoms se subscribe por medio del método nativo `AddEventListener` a los citados eventos y una vez se ejecuten los publica con *Bubbling* en nuestra aplicación.

Ahora vamos a crear un método de instancia que nos devuelva el valor actual del `Atom.Input`, para ello necesitarémos hacer referencia al elemento del DOM por medio del simbolo `@el`:

```
class Atoms.Atom.Input extends Atoms.Class.Atom
  ... 
  
  value: (value) ->
    if value? then @el.val value else @el.val()
```

Como puedes leer el método de instancia `value`, realiza dos funciones: establecer un nuevo valor a la instancia o recoger el actual. Veamos como funciona:

```
number = new Atoms.Atom.Input type: "number", value: 1980
number.value()
> 1980

number.value 1983
number.value()
> 1983

number.el.html()
> <input type="number" value="1983" />
```

Bien, ahora vamos a crear otro método de instancia que controle la visualización en el caso de que el valor introducimos no sea el correcto, lo llamaremos `error`:

```
class Atoms.Atom.Input extends Atoms.Class.Atom
  ... 
  
  error: (value) ->
    method = if value then "addClass" else "removeClass"
    @el[method] "error"
```

Con el método `error` conseguimos añadir la clase `"error"` en el caso de que se le pase un `true` como parametro, en caso contrario eliminará toda señal de esta clase. Con esto finalizamos la creación de nuestro primer elemento *Atom*, `Atom.Input` es capaz de:

  - Representar todo tipo de `<input/>`, por defecto `"text"`.
  - Subscribirse a los eventos nativos: `"keypress"` y `"keyup"`.
  - Extender el objeto DOM dos nuevos métodos de instancia `value` y `error`.


### 7.3 - Atom.Button
El segundo elemento *Atom* que vamos a crear es el que representa la etiqueta `<button/>` el cual dentro de un formulario se encarga del envio de los valores (normalmente `<input/>`) del mismo. En nuestro caso vamos a crear `Atom.Button`:

```
class Atoms.Atom.Button extends Atoms.Class.Atom

  @template : """
    <button class="{{style}}{{^if.text}} icon{{/if.text}}" {{#if.disabled}}disabled{{/if.disabled}}>
      {{#if.icon}}<span class="icon {{icon}}"></span>{{/if.icon}}
      {{#if.text}}<abbr>{{text}}</abbr>{{/if.text}}
    </button>"""

  @base     : "Button"

  @events   : ["touch"]

  @default  :
    events: ["touch"]
```

Repasando la definición completa de nuestro nuevo Atom, vemos que para la representación en el DOM podemos utilizar los atributos: "text", "icon", "style" y "disabled", asi como el único evento soportado será "touch" de [QuoJS](http://quojs.tapquo.com) el cual será automáticamente suscrito con cada nueva instancia y desplegado por medio de *Bubbling* dentro de tu aplicación Atoms.

Como puedes comprobar la definición de nuevos elementos es realmente expresiva y parametrizable, ahora vamos a crear un par de instancias para ver si representa lo que buscamos:

```
login = new Atoms.Atom.Button text: "login"
login.el.html()
> <button><abbr>login</abbr></button>

signup = new Atoms.Atom.Button icon: "add", disabled: true, style: "small"
signup.el.html()
> <button class="small icon" disabled><span class="icon add"></span></button>
```

En este caso, y tras comprobar que todo funciona como esperamos, y en comparación con `Atom.Input` no necesitamos definir ningun comportamiento más por lo que podemos pasar al siguiente capítulo.


### 7.4 - Molecule.Form
Ya tenemos nuestros elementos `Atom.Input` y `Atom.Button`, los cuales autogestionan su representación en el DOM y despliegan eventos *Bubble* a su jerarquia superior, que en nuestro caso va a ser un elemento *Molecule* llamado `Molecule.Form`. Comencémos con su definición:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule

  @template : """<form></form>"""

  @available: ["Atom.Input", "Atom.Button"]

  @events   : ["change", "submit", "error"]

  @base     : "Form"

  @default  :
    events: ["submit"]
```

Analizando la definición de `Molecule.Form` podemos decir:

  - Como podemos ver en `@template` no acepta ningun atributo para su representación en el DOM. 
  - Solo permite almacenar los elementos *Atom* que hemos creado `Atom.Input` y `Atom.Button`.
  - Desplegará tres eventos bubble: `"change"`, `"submit"` y `"error"`, por defecto `"submit"`.

Antes de crear nuestra primera instancia vamos a sobreescribir la funcionalidad nativa de todo `<form/>` cuando se ejecuta el evento nativo `"submit"`, para ello vamos a subscribirnos a ese evento y evitarlo:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  constructor: ->
    super
    @el.bind "submit", (event) -> event.preventDefault()
```

Realmente es sencillo, simplemente tenemos que sobreescribir el constructor extendido de `Atoms.Class.Molecule` dejando que ejecute todo lo establecido en el y añadiendo el bloqueo del evento `"submit"`. Con esto conseguimos que si tu aplicación es del tipo *SPA* (Single Page Application) no haga un POST del formulario, el cual en muchas ocasiones se traduce en un refresco de la aplicación.

Ahora vamos a continuar con la creación de un método de instancia value, que recogerá todos los valores de los `children` que tenga asignados. Recordemos que anteriormente tambien definimos un método `value` en el `Atom.Input`, ahora verémos como sacarle provecho:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  value: ->
    properties = {}
    for child in @children when child.attributes.name and child.value?
      properties[child.attributes.name.toLowerCase()] = child.value()
    properties
```

Si analizamos el código de nuestro metodo `value` vemos que nos recorremos el simbolo `@children` el cual contiene todas las instancias de elementos *Atom* que contiene nuestra instancia de `Molecule.Form`. En el iterador for vemos como solo recorremos los child que tengan un atributo `name` y tengan tambien el método `value`, por cada uno de ellos creamos un atributo dentro del objeto properties utilizando como `key` el `name` y el `value` el valor resultante del método `value`. El método `value` finaliza con la devolución de un objeto con todos los valores recogidos en el formulario, veamos un ejemplo:

```
form = new Atoms.Molecule.Form()
form.appendChild "Atom.Input", name: "usermane", value: "@soyjavi"
form.appendChild "Atom.Input", type "password", name: "password"
form.appendChild "Atom.Input", type "number", value: "1980"

form.value()
> {username: "@soyjavi", password: ""}
```

Como podemos comprobar con la ejecución de `value` nos devuelve un objeto recogiendo los valores de los `Atom.Input` que tengan un atributo `name`. Esta acción es algo muy común (y laborioso) en la gestión de formularios en Aplicaciones Web, y nosotros acabamos de crear un elemento *Molecule* que hace ese trabajo por nosotros y como aprenderémos más tarde podrémos reutilizarlo en las futuras extensiones de `Molecule.Form`. Ahora vamos a crear otro método que *limpie* todos los valores recogidos en los `Atom.Input`:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  clean: ->
    child.value "" for child in @children when child.attributes.name and child.value?
    true
```

Como vemos clean realiza una iteración como en `value`, busca los `children` que tienen atributo `name` y método `value` y por cada uno de ellos ejecuta su método de instancia `value`.

Ahora vamos a gestionar tanto los eventos *Bubble* que despliegan los children asi como los que tiene que desplegar nuestro `Molecule.Form`. Para ello vamos a comenzar con la suscripción de los eventos de nuestros `children`, los cuales no dejaremos que continuen con el *Bubbling* devolviendo un `false` al finalizar su ejecución: 

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  onInputKeypress: (event, atom) =>
    false

  onInputKeyup: (event, atom) =>
    false

  onButtonTouch: (event, atom) =>
    false
```

Como vemos nos suscribimos a los eventos *Bubble*  por medio de los *handlers* `onInputKeypress`, `onInputKeyup` y `onButtonTouch`, los dos primeros desplegarán el evento de nuestro *Molecule* `"change"` y además ejecutarán el método de instancia `error` que creamos en `Atom.Input`:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  onInputKeypress: (event, atom) =>
    @_bubbleChange event, atom

  onInputKeyup: (event, atom) =>
    @_bubbleChange event, atom
  
  _bubbleChange: (event, atom) ->
    event.preventDefault()
    is_invalid = (atom.attributes.required and not atom.value())
    atom.error is_invalid
    @bubble "change", event
    false
```

Como podemos ver creamos una variable booleana `is_invalid` que define si el campo es obligatorio gracias al atributo `required` y el método `value`, en caso de que lo sea y no tenga valor establecerá la clase *CSS* `"error"` en nuestra instancia de `Atom.Input`. De una manera u otra nuestra molecula desplegará el evento `"change"` gracias al evento interno `@bubble` pasando como información de evento el mismo que recogía del `Atom.Input`. Señalar que  este evento solo se ejecutará cuando las instancias de `Atom.Input` tenga asignadas los eventos `"keypress"` o `"keyup"` ya que es la unica manera de llegar a los *handlers* definidos en nuestra *Molecule*.

Ahora vamos a gestionar nuestro *handler* `onButtonTouch` el cual desplegará los otros dos eventos que teníamos definidos para `Molecule.Form`: `"submit"` y `"error"`. El primero se desplegará si todos los campos *required* tienen valor, mientras que el segundo se desplegará en caso contrario:

```
class Atoms.Molecule.Form extends Atoms.Class.Molecule
  ...
  
  onButtonTouch: (event, atom) =>
    event.preventDefault()
    required = true
    for child in @children when child.attributes.required and not child.value?()
      required = false
      break
      
    method = if required then "submit" else "error"
    @bubble method, event
    false
```

Con esto hemos finalizado por completo la definición de nuestra primera extension en Atoms, como has podido comprobar es realmente sencillo y lo mejor de todo sigue dándonos la posibilidad de reutilizar tanto `Atom.Input` y `Atom.Button` en otros elementos *Molecule* asi como seguir extendiendo `Molecule.Form` como verémos en la siguiente sección con `Molecule.Search`.


### 7.5 - Extendiendo Molecule.Form

```
class Atoms.Molecule.Search extends Atoms.Molecule.Form

  @events   : ["change", "submit"]

  @extends  : true

  @default  :
    events  : ["submit"]
    children: [
      "Atom.Input": id: "input", type: "search", placeholder: "Type your search...", events: ["keypress", "keyup"], required: true
    ,
      "Atom.Button": icon: "search"
    ]
    
  value: (value) ->
    @input.value value or null
    
  onInputKeyup: (event, atom) =>
    @bubble "change", event unless event.keyCode is 13
    false

  onInputKeypress: (event, atom) =>
    @_bubbleSubmit event, atom if event.keyCode is 13
    false
    
  onButtonTouch: (event, atom) =>
    event.preventDefault()
    @_bubbleSubmit event, atom
    false

  _bubbleSubmit: (event, atom) ->
    atom?.el?[0].blur()
    value = @input.value()
    @bubble "submit", event if value isnt ""
```

### 7.6 - Atoms.App 
Como hemos dicho en varias ocasiones Atoms pretende ofrecerte una capa de abstracción a la hora de crear tus aplicaciones, por ello desde tapquo hemos creado una extensión [Atoms-App](https://github.com/tapquo/atoms-app) la cual contiene una serie de elementos *Atom*, *Molecule* y *Organism* que te facilitarán la creación.

Tanto si la utilizas como si creas la tuya propia vale la pena tenerla como herramienta de estudio ya que podrás encontrar una gran cantidad de extensiones divididas por contexto:
  
  - *Atom*: `Audio`, `Button`, `Figure`, `Heading`, `Icon`, `Image`, `Input`, `Label`, `Li`, `Link`, `Select`, `Switch`, `Textarea`...
  - *Molecule*: `Div`, `Form`, `List`, `Navigation`, `Search`, `Tags`
  - *Organism*: `Article`, `Aside`, `Footer`, `Header`, `Modal`, `Section` 
  
Recordarte que puedes colaborar con la extension [Atoms-App](https://github.com/tapquo/atoms-app) aportando nuevos elementos o creando nuevas funcionalidades.